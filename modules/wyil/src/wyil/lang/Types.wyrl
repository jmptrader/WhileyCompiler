// Copyright (c) 2011, David J. Pearce (djp@ecs.vuw.ac.nz)
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//    * Redistributions of source code must retain the above copyright
//      notice, this list of conditions and the following disclaimer.
//    * Redistributions in binary form must reproduce the above copyright
//      notice, this list of conditions and the following disclaimer in the
//      documentation and/or other materials provided with the distribution.
//    * Neither the name of the <organization> nor the
//      names of its contributors may be used to endorse or promote products
//      derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
// ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
// WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL DAVID J. PEARCE BE LIABLE FOR ANY
// DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
// (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
// LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
// SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

package wyil.lang

// ====================================================================
// Overview
// ====================================================================
//
// Defines a language of types similar to that found in this paper:
//
// * "Sound and Complete Flow Typing with Unions, Intersections and
//    Negations", David J. Pearce, In Proceedings of VMCAI, 2013.

// ====================================================================
// Not
// ====================================================================

term NotT(Type)

reduce NotT(AnyT):
    => VoidT

reduce NotT(VoidT):
    => AnyT

reduce NotT(NotT(Type t)):
    => t

reduce NotT(OrT{Type... es}):
    => let ns = { NotT(e) | e in es }
       in AndT(ns)

reduce NotT(AndT{Type... es}):
    => let ns = { NotT(e) | e in es }
       in OrT(ns)

// ====================================================================
// And
// ====================================================================

term AndT{Type...}

reduce AndT{}:
    => VoidT

reduce AndT{Type t}:
    => t

reduce AndT{NotT(Type t1), Type t2, Type... ys}
requires t1 == t2:
    => VoidT

reduce AndT{AndT{Type... xs}, Type... ys}:
    => AndT (xs ++ ys)

reduce AndT{OrT{Type... xs}, Type... ys}:
    => let zs = { AndT(x ++ ys) | x in xs }
       in OrT (zs)

// ====================================================================
// Or
// ====================================================================

term OrT{Type...}

reduce OrT{}:
    => VoidT

reduce OrT{Type t}:
    => t

reduce OrT{OrT{Type... xs}, Type... ys}:
    => OrT (xs ++ ys)

// ====================================================================
// Record
// ====================================================================
term RecordT{[string,Type]...}

reduce RecordT{[string,VoidT], [string,Type]... ts}:
    => VoidT

reduce AndT{RecordT{[string,Type]... t1s},
            RecordT{[string,Type]... t2s},
            Type... ts}
requires |t1s| != |t2s|:
    => VoidT

reduce AndT{RecordT{[string,Type]... t1s},
            RecordT{[string,Type]... t2s},
            Type... ts}
requires |t1s| == |t2s| && { p[0] | p in t1s } != { p[0] | p in t2s }:
    => VoidT

reduce AndT{RecordT{[string,Type]... t1s},
            RecordT{[string,Type]... t2s},
            Type... ts}
requires |t1s| == |t2s| && { p[0] | p in t1s } == { p[0] | p in t2s }:
    => let f1s = { [p1[0],AndT{p1[1],p2[1]}] | p1 in t1s, p2 in t2s, p1[0]==p2[0] }
       in AndT(RecordT(f1s)++ts)

reduce AndT{RecordT{[string,Type]... t1s} r,
            NotT(RecordT{[string,Type]... t2s}),
            Type... ts}
requires |t1s| != |t2s|:
    => AndT(r ++ ts)

reduce AndT{RecordT{[string,Type]... t1s} r,
            NotT(RecordT{[string,Type]... t2s}),
            Type... ts}
requires |t1s| == |t2s| && { p[0] | p in t1s } != { p[0] | p in t2s }:
    => AndT(r ++ ts)

reduce AndT{RecordT{[string,Type]... t1s},
            NotT(RecordT{[string,Type]... t2s}),
            Type... ts}
requires |t1s| == |t2s| && { p[0] | p in t1s } == { p[0] | p in t2s }:
    => let f1s = { [p1[0],AndT{p1[1],NotT(p2[1])}] | p1 in t1s, p2 in t2s, p1[0]==p2[0] }
       in AndT(RecordT(f1s)++ts)

// TODO: pull disjuncts out of records?

// ====================================================================
// Array
// ====================================================================
term ArrayT(Type)

reduce ArrayT(VoidT):
    => VoidT

reduce AndT{ArrayT(Type t1),
            ArrayT(Type t2),
            Type... ts}:
    => AndT(ArrayT(AndT{t1,t2})++ts)

reduce AndT{ArrayT(Type t1),
            NotT(ArrayT(Type t2)),
            Type... ts}:
    => AndT(ArrayT(AndT{t1,NotT(t2)})++ts)

reduce AndT{ArrayT(Type) t1,
           NotT(RecordT),
           Type... ts}:
    => AndT(t1 ++ ts)

reduce AndT{ArrayT(Type) s, Proton p, Type... ts}
requires !(p is AnyT):
    => VoidT

reduce OrT{ArrayT(Type t1) s1, ArrayT(Type t2) s2, Type... ts}
requires t1 == t2:
    => OrT(s1 ++ ts)

// ====================================================================
// References
// ====================================================================
term ReferenceT(Type)

reduce ReferenceT(VoidT):
    => VoidT

reduce AndT{ReferenceT(Type t1),
            ReferenceT(Type t2),
            Type... ts}
requires t1 == t2:
    => AndT(ReferenceT(t1)++ts)

reduce AndT{ReferenceT(Type t1),
            ReferenceT(Type t2),
            Type... ts}
requires t1 != t2:
    => AndT(ReferenceT(t1)++ts)

reduce AndT{ReferenceT(Type t1) r,
            NotT(ReferenceT(Type t2)),
            Type... ts}
requires t1 != t2:
    => AndT(r ++ ts)

// ====================================================================
// Function
// ====================================================================
term FunctionT[bool,[Type...],[Type...]]

reduce FunctionT[bool,[Type... from],[Type... to]]
requires VoidT in from || VoidT in to:
    => VoidT

reduce AndT{FunctionT[bool p1,[Type... f1s],[Type... t1s]],
            FunctionT[bool p2,[Type... f2s],[Type... t2s]],
            Type... ts}
requires p1 != p2:
    => VoidT

reduce AndT{FunctionT[bool p1,[Type... f1s],[Type... t1s]],
            FunctionT[bool p2,[Type... f2s],[Type... t2s]],
            Type... ts}
requires p1 == p2:
    => VoidT, if |f1s| != |f2s| || |t1s| != |t2s|
    => let from = [ AndT{f1s[i],f2s[i]} | i in 0..|f1s|],
           to   = [ OrT{t1s[i],t2s[i]} | i in 0..|t1s|]
       in AndT(FunctionT[p1,from,to] ++ ts)

reduce AndT{FunctionT[bool p1,[Type... f1s],[Type... t1s]] f,
            NotT(FunctionT[bool p2,[Type... f2s],[Type... t2s]]),
            Type... ts}
requires p1 != p2:
    => AndT(f ++ ts)

reduce AndT{FunctionT[bool p1,[Type... f1s],[Type... t1s]] f,
            FunctionT[bool p2,[Type... f2s],[Type... t2s]],
            Type... ts}
requires p1 == p2:
    => AndT(f ++ ts), if |f1s| != |f2s| || |t1s| != |t2s|
    // FIXME: need another rule here

// ====================================================================
// Atoms
// ====================================================================

term AnyT
term VoidT
term NullT
term BoolT
term ByteT
term IntT
term NominalT(string)

define Quark as AnyT | NullT | VoidT | BoolT | ByteT | IntT | NominalT(string)
define Proton as Quark | RecordT{[string,Proton]...} | ArrayT(Proton) | ReferenceT(Proton) | FunctionT[bool,[Proton...],[Proton...]]
define Atom as Proton | NotT(Proton)

reduce AndT{VoidT, Type... xs}:
    => VoidT

reduce AndT{AnyT, Type... xs}:
    => AndT(xs)

// a1 needs to be a quark here, as the rule for tuples is handled
// above.
reduce AndT{Quark a1, Proton a2, Type... ts}
requires a1 != a2 && a1 != AnyT && a2 != AnyT:
    => VoidT

// a1 needs to be a quark here, as the rule for tuples is handled
// above.
reduce AndT{Quark a1, NotT(Proton a2), Type... ts}
requires a1 == a2:
    => VoidT

reduce AndT{Quark a1, NotT(Proton a2), Type... ts}
requires a1 != a2 && a2 != AnyT:
    => AndT (a1 ++ ts)

reduce OrT{AnyT, Type... xs}:
    => AnyT

reduce OrT{VoidT, Type... xs}:
    => OrT(xs)


// ====================================================================
// General Type
// ====================================================================

define Type as Atom | NotT | OrT | AndT | ArrayT | ReferenceT | RecordT | FunctionT

